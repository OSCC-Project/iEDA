WHITESPACE = _{ " " | "\t" | "\r" }

int = _{ ("+" | "-")? ~ ASCII_DIGIT+ }
num = @{ int ~ ("." ~ ASCII_DIGIT*)? ~ (^"e" ~ int)? }
index = ${ ASCII_DIGIT+ }

line_comment      = _{ "//" ~ (!("\n") ~ ASCII)* ~ ("\n" | EOI) }
multiline_comment = _{ "/*" ~ (!"*/" ~ ANY)* ~ "*/" }

COMMENT = _{ line_comment | multiline_comment }

header_char = _{ ASCII_ALPHANUMERIC | "_" | "\\" | "/" | "[" | "]" | ","| "\"" | "-" | ":" | "." }
char = _{ ASCII_ALPHANUMERIC | "_" | "\\" | "/" | "[" | "]" | ","| "\""}

section = @{ "*" ~ section_name }
section_name = @{ "NAME_MAP" | "PORTS" | "CONN" | "CAP" | "RES" | "END" }

header_entry = { header_keywords ~ header_value }
header_keywords = { "*SPEF" | "*DESIGN" | "*DATE" 
    | "*VENDOR" | "*PROGRAM" | "*VERSION" 
    | "*DESIGN_FLOW" | "*DIVIDER" | "*DELIMITER" 
    | "*BUS_DELIMITER" | "*T_UNIT" | "*C_UNIT" 
    | "*R_UNIT" | "*L_UNIT" 
}
header_value = { !direction ~ header_char+ }

name_map_entry = { index_name ~ !direction ~ str_name }
str_name = @{ char+ }
index_name = ${ "*" ~ index }

ports_entry = { index_name ~ direction ~ coordinate_param ~ xy_coordinates }
direction = { "I" | "O" | "B" }
coordinate_param = _{ "*C" }
xy_coordinates = { num{2} }

dnet_entry = { "*D_NET" ~ index_name ~ cap_val }
cap_val = _{ num{1} }

conn_entry = { conn_type ~ pin_port ~ direction ~ coordinate_param ~ xy_coordinates ~ (load_param ~ cap_val)? ~ (drive_param ~ str_name)? }
conn_type = { "*P" | "*I" | "*S" | "*C" | "*R" | "*L" }
pin_port = ${ index_name ~ (":" ~ pin_name)? }
pin_name = { (ASCII_ALPHANUMERIC | "_")+ }
load_param = _{ "*L" }
drive_param = _{ "*D" }

cap_entry = { index ~ pin_port{1} ~ cap_val }

res_entry = { index ~ pin_port{2} ~ res_val }
res_val = { num{1} }

file = _{
    SOI ~
    (section | header_entry | name_map_entry | ports_entry | dnet_entry | conn_entry | cap_entry | res_entry | NEWLINE )* ~
    EOI
}